apply plugin: 'com.android.application'

android {
    signingConfigs {
        signConfig {
            keyAlias 'key0'
            keyPassword '5526374891'
            storeFile file('C:/Users/User/keystores/android.jks')
            storePassword '5526374891'
        }
    }
    def getVersionName = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--long'
            standardOutput = stdout
        }
        String[] temp = stdout.toString().trim().split("-")

        return temp[0] + '.' + temp[1]
    }
    def getVersionCode = { ->
        String[] temp = getVersionName().split("\\.")

        return temp[0].toInteger() * 10000 + temp[1].toInteger() * 100 + temp[2].toInteger()
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.user.androidlabs"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode getVersionCode()
        versionName getVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    setProperty("archivesBaseName", "com.example.user.androidlabs" + defaultConfig.versionName)
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.signConfig
        }
        debug {
        }
    }
    flavorDimensions "version"
    productFlavors {
        general {
            manifestPlaceholders.screenOrientation = "portrait"
        }
        dev {
            manifestPlaceholders.screenOrientation = "unspecified"
            versionNameSuffix "-dev"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1-alpha01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1-alpha01'
}
